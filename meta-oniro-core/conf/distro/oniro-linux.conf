# SPDX-FileCopyrightText: Huawei Inc.
#
# SPDX-License-Identifier: Apache-2.0

DISTRO = "oniro-linux"
DISTRO_NAME = "Oniro Project Base Linux Distro"
DISTRO_VERSION = "1.99.99"
DISTRO_CODENAME = "dev"
SDK_VENDOR = "-onirosdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${DATE}', 'snapshot')}"

MAINTAINER = "Oniro Project <andrei.gherzan@huawei.com>"

TARGET_VENDOR = "-oniro"

LOCALCONF_VERSION = "2"

DISTRO_VERSION[vardepsexclude] = "DATE"
SDK_VERSION[vardepsexclude] = "DATE"

# Override these in oniro based distros
ONIRO_DEFAULT_DISTRO_FEATURES = "largefile opengl ptest multiarch pam rauc security wayland vulkan"
ONIRO_DEFAULT_EXTRA_RDEPENDS = "packagegroup-core-boot"
ONIRO_DEFAULT_EXTRA_RRECOMMENDS = "kernel-module-af-packet"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${ONIRO_DEFAULT_DISTRO_FEATURES}"

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_VERSION}"

DISTRO_EXTRA_RDEPENDS += " ${ONIRO_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += " ${ONIRO_DEFAULT_EXTRA_RRECOMMENDS}"

ONIROQEMUDEPS = "${@bb.utils.contains("INCOMPATIBLE_LICENSE", "GPL-3.0", "", "packagegroup-core-device-devel",d)}"
DISTRO_EXTRA_RDEPENDS_append_qemuarm = " ${ONIROQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemuarm64 = " ${ONIROQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemumips = " ${ONIROQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemuppc = " ${ONIROQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemux86 = " ${ONIROQEMUDEPS}"
DISTRO_EXTRA_RDEPENDS_append_qemux86-64 = " ${ONIROQEMUDEPS}"

TCLIBCAPPEND = ""

PREMIRRORS ??= "\
bzr://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
cvs://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
git://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
gitsm://.*/.* http://downloads.yoctoproject.org/mirror/sources/ \n \
hg://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
osc://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
p4://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
svn://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n"

SANITY_TESTED_DISTROS ?= " \
            ubuntu-16.04 \n \
            ubuntu-18.04 \n \
            ubuntu-19.04 \n \
            ubuntu-20.04 \n \
            fedora-30 \n \
            fedora-31 \n \
            fedora-32 \n \
            fedora-33 \n \
            centos-7 \n \
            centos-8 \n \
            debian-8 \n \
            debian-9 \n \
            debian-10 \n \
            opensuseleap-15.1 \n \
            opensuseleap-15.2 \n \
            "

# QA check settings - a little stricter than the OE-Core defaults
WARN_TO_ERROR_QA = "already-stripped compile-host-path install-host-path \
                    installed-vs-shipped ldflags pn-overrides rpaths staticdev \
                    unknown-configure-option useless-rpaths"
WARN_QA_remove = "${WARN_TO_ERROR_QA}"
ERROR_QA_append = " ${WARN_TO_ERROR_QA}"

require conf/distro/include/no-static-libs.inc
require conf/distro/include/yocto-uninative.inc
require conf/distro/include/security_flags.inc
INHERIT += "uninative"

INHERIT += "reproducible_build"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"

require conf/distro/include/oniro.inc
require conf/distro/include/oniro-wic.inc
require conf/distro/include/oniro-packageconfig.inc

TCLIBC ?= "musl"

INIT_MANAGER = "systemd"

# Remove some of the default Poky DISTRO_FEATURES we inherited, but do not use.
DISTRO_FEATURES_remove = " nfs nfc 3g pcmcia"

# Remove some of the default Poky IMAGE_FEATURES we inherited, but do not need.
IMAGE_FEATURES_remove = " nfs-server nfs-client nfs-utils"

# Remove some of the default Poky runtime dependencies that have no use
DISTRO_EXTRA_RDEPENDS_remove = " packagegroup-core-device-devel"

PREFERRED_VERSION_linux-raspberrypi = "5.10.%"

PREFERRED_VERSION_linux-yocto = "5.10%"
PREFERRED_PROVIDER_virtual/kernel_qemuarm = "linux-oniro"
PREFERRED_PROVIDER_virtual/kernel_qemuarm64 = "linux-oniro"
PREFERRED_PROVIDER_virtual/kernel_qemux86 = "linux-oniro"
PREFERRED_PROVIDER_virtual/kernel_qemux86-64 = "linux-oniro"

# qemu-generic-arm64 has issues booting past 5.10. See:
# https://git.yoctoproject.org/meta-arm/tree/meta-arm/conf/machine/generic-arm64.conf?id=c40fb5348b1d0f8c4a1ed779c8df6ba3cf411930
# When kernel version is bumped we will have to revisit here.
PREFERRED_PROVIDER_virtual/kernel_qemu-generic-arm64 = "linux-oniro"

# qemu-generic-arm64 specific requirements
PREFERRED_VERSION_optee-os_qemu-generic-arm64 = "3.14.0"

# Default to enabling serial debug console on RaspberryPi
ENABLE_UART ?= "1"

# The KMS driver for RPI4 requires a different vc4 overlay for Raspberry Pi 4.
# This is done dynamically through overlay_map.dtb. More info:
# https://www.raspberrypi.com/documentation/computers/configuration.html#part2.2.10
RPI_KERNEL_DEVICETREE_OVERLAYS_append = " \
    overlays/overlay_map.dtb \
    overlays/vc4-kms-v3d-pi4.dtbo \
"
IMAGE_BOOT_FILES_remove_rpi = "overlay_map.dtb"
IMAGE_BOOT_FILES_append_rpi = " overlay_map.dtb;overlays/overlay_map.dtb"

VC4DTBO_raspberrypi4-64 = "vc4-kms-v3d"
GPU_MEM_raspberrypi4-64 = "128"

SPLASH = "psplash-oniro"
